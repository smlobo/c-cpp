
extern putchard(x)

def binary: 1 (x y)
    0

def unary! (v)
    if v then
        0
    else
        1

def unary- (v)
    0 - v

def binary> 10 (LHS RHS)
  RHS < LHS

def binary| 5 (LHS RHS)
    if LHS then
        1
    else if RHS then
        1
    else
        0

def binary& 6 (LHS RHS)
    if !LHS then
        0
    else if !RHS then
        0
    else
        1

def binary= 9 (LHS RHS)
    if LHS < RHS then
        0
    else if LHS > RHS then
        0
    else
        1

def printdensity(d)
    if d > 8 then
        putchard(32)  # ' '
    else if d > 4 then
        putchard(46)  # '.'
    else if d > 2 then
        putchard(43)  # '+'
    else
        putchard(42); # '*'

def foo()
    printdensity(1): printdensity(2): printdensity(3):
           printdensity(4): printdensity(5): printdensity(9):
           putchard(10);

# Determine whether the specific location diverges.
# Solve for z = z^2 + c in the complex plane.
def mandelconverger(real imag iters creal cimag)
    if iters > 255 | (real*real + imag*imag > 4) then
        iters
    else
        mandelconverger(real*real - imag*imag + creal,
                    2*real*imag + cimag,
                    iters+1, creal, cimag);

# Return the number of iterations required for the iteration to escape
def mandelconverge(real imag)
    mandelconverger(real, imag, 0, real, imag);

# Compute and plot the mandelbrot set with the specified 2 dimensional range
# info.
def mandelhelp(xmin xmax xstep   ymin ymax ystep)
    for y = ymin, y < ymax, ystep in (
        (for x = xmin, x < xmax, xstep in
            printdensity(mandelconverge(x,y))
        )
        : putchard(10)
    )

# mandel - This is a convenient helper function for plotting the mandelbrot set
# from the specified position with the specified Magnification.
def mandel(realstart imagstart realmag imagmag)
    mandelhelp(realstart, realstart+realmag*78, realmag,
               imagstart, imagstart+imagmag*40, imagmag);
